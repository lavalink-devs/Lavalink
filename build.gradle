buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
        gradleGitVersion = '1.5.2'
        sonarqubeVersion = '2.6.2'
        kotlinVersion = '1.2.71'
        testLoggerVersion = '1.6.0'
        versionsPluginVersion = '0.21.0'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarqubeVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:$testLoggerVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versionsPluginVersion"
    }
}

allprojects {

    repositories {
        mavenCentral()                     // main maven repo
        jcenter()                          // JDA and some other stuff
        mavenLocal()                       // useful for developing
        maven { url "https://jitpack.io" } // build projects directly from github
    }

    group = 'lavalink'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.github.ben-manes.versions'

    if (project.hasProperty("includeAnalysis")) {
        project.logger.lifecycle("applying analysis plugins")
        apply from: "../analysis.gradle"
    }

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    ext {
        //@formatter:off

        lavaplayerVersion               = '1.3.17'
        magmaVersion                    = '0.9.0'
        jdaNasVersion                   = '1.0.6'
        jappVersion                     = '1.3'

        springBootVersion               = "${springBootVersion}"
        springWebSocketVersion          = '5.0.8.RELEASE'
        logbackVersion                  = '1.2.3'
        sentryLogbackVersion            = '1.7.7'
        oshiVersion                     = '3.13.0'
        jsonOrgVersion                  = '20180813'
        spotbugsAnnotationsVersion      = '3.1.6'
        prometheusVersion               = '0.5.0'
        commonsLangVersion              = '3.8'

        //@formatter:on
    }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

ext {
    moduleName = 'Lavalink-Parent'
}
