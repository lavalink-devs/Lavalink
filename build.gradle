buildscript {
    ext {
        springBootVersion = '2.1.8.RELEASE'
        gradleGitVersion = '1.5.2'
        sonarqubeVersion = '2.6.2'
        kotlinVersion = '1.3.41'
        testLoggerVersion = '1.6.0'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarqubeVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:$testLoggerVersion"
    }
}

allprojects {

    repositories {
        mavenCentral()                     // main maven repo
        jcenter()                          // JDA and some other stuff
        mavenLocal()                       // useful for developing
        maven { url "https://jitpack.io" } // build projects directly from github
        maven { url 'https://dl.bintray.com/sedmelluq/com.sedmelluq' }
    }

    group = 'lavalink'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    if (project.hasProperty("includeAnalysis")) {
        project.logger.lifecycle("applying analysis plugins")
        apply from: "../analysis.gradle"
    }

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    ext {
        //@formatter:off
        lavaplayerVersion               = '1.3.32'
        lavaplayerIpRotatorVersion      = '0.1.7'
        magmaVersion                    = '0.12.3'
        jdaNasVersion                   = '1.1.0'
        jappVersion                     = '1.3.2-MINN'

        springBootVersion               = "${springBootVersion}"
        springWebSocketVersion          = '5.1.9.RELEASE'
        logbackVersion                  = '1.2.3'
        sentryLogbackVersion            = '1.7.7'
        oshiVersion                     = '3.13.3'
        jsonOrgVersion                  = '20180813'
        spotbugsAnnotationsVersion      = '3.1.6'
        prometheusVersion               = '0.5.0'
        commonsLangVersion              = '3.8'

        //@formatter:on
    }
}

ext {
    moduleName = 'Lavalink-Parent'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}
